plugins {
    id 'net.kyori.blossom' version '1.3.1'
}

allprojects {
    processResources {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        from(sourceSets.main.resources.srcDirs) {
            include '**/*.yml'
            expand(project.properties)
        }
    }
}

subprojects {
    apply plugin: 'com.gradleup.shadow'

    dependencies {
        implementation project(':plugin')
    }

    jar {
        enabled = false
        dependsOn (shadowJar)
    }

    shadowJar {
        archiveBaseName.set(rootProject.name + '-' + project.name.substring(7))
        archiveClassifier.set('')
        manifest {
            attributes 'Paperweight-Mappings-Namespace': 'mojang'
        }
        destinationDirectory.set(file(rootProject.layout.buildDirectory.dir('libs')))
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.shadow
            }
        }
    }
}

blossom {
    replaceTokenIn('src/main/java/com/saicone/item/render/ItemRenderLoader.java')
    replaceToken '${dependency_group}', ((System.getenv("GROUP") ?: "com.saicone") + "." + (System.getenv("ARTIFACT") ?: "itemrender")).replace('.', '{}')
    replaceToken '${dependency_version}', System.getenv("VERSION") ?: project.version
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
}

dependencies {
    compileOnly('org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT') {
        exclude group: 'com.mojang'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}