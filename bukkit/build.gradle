allprojects {
    apply plugin: 'com.gradleup.shadow'

    repositories {
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
        maven { url 'https://repo.codemc.io/repository/nms/' }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    jar {
        enabled = false
        dependsOn (shadowJar)
    }

    shadowJar {
        archiveClassifier.set('')
    }

    if (project.name != 'stickynote') {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId project.group
                    artifactId project.name
                    version project.version
                    from components.shadow
                }
            }
        }
    }
}

def modules = [
        'bukkit-1.8.8': [ depends: null, package: '.v1_8_R3' ],
        'bukkit-1.9.2': [ depends: ':bukkit:bukkit-1.8.8', package: '.v1_9_R1' ],
        'bukkit-1.9.4': [ depends: ':bukkit:bukkit-1.9.2', package: '.v1_9_R2' ],
        'bukkit-1.10.2': [ depends: ':bukkit:bukkit-1.9.2', package: '.v1_10_R1' ],
        'bukkit-1.11.2': [ depends: ':bukkit:bukkit-1.9.2', package: '.v1_11_R1' ],
        'bukkit-1.12.2': [ depends: ':bukkit:bukkit-1.11.2', package: '.v1_12_R1' ],
        'bukkit-1.13': [ depends: ':bukkit:bukkit-1.12.2', package: '.v1_13_R1' ],
        'bukkit-1.13.2': [ depends: ':bukkit:bukkit-1.13', package: '.v1_13_R2' ],
        'bukkit-1.14.4': [ depends: ':bukkit:bukkit-1.13', package: '.v1_14_R1' ],
        'bukkit-1.15.2': [ depends: ':bukkit:bukkit-1.14.4', package: '.v1_15_R1' ],
        'bukkit-1.16.1': [ depends: ':bukkit:bukkit-1.14.4', package: '.v1_16_R1' ],
        'bukkit-1.16.3': [ depends: ':bukkit:bukkit-1.16.1', package: '.v1_16_R2' ],
        'bukkit-1.16.5': [ depends: ':bukkit:bukkit-1.16.1', package: '.v1_16_R3' ]
]

subprojects {
    def module = modules[project.name]

    if (module == null) {
        return
    }

    if (module.depends != null) {
        dependencies {
            api project(module.depends.toString())
        }
    }

    def toPackage1 = 'org.bukkit.craftbukkit' + module.package
    def toPackage2 = 'net.minecraft.server' + module.package
    shadowJar {
        for (final def entry in modules.entrySet()) {
            if (entry.key == project.name) {
                break
            }
            def fromPackage1 = 'org.bukkit.craftbukkit' + entry.value.package
            def fromPackage2 = 'net.minecraft.server' + entry.value.package
            relocate fromPackage1, toPackage1
            relocate fromPackage2, toPackage2
        }
    }
}

dependencies {
    api rootProject
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'
}

java {
    withJavadocJar()
}