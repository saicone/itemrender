description = 'ItemRender Bukkit'

allprojects {
    apply plugin: 'com.gradleup.shadow'

    repositories {
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
        maven { url 'https://repo.codemc.io/repository/nms/' }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    jar {
        enabled = false
        dependsOn (shadowJar)
    }

    shadowJar {
        archiveClassifier.set('')
    }

    if (project.name != 'stickynote') {
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.shadow

                    afterEvaluate {
                        artifact tasks.named('sourcesJar')
                        artifact tasks.named('javadocJar')
                    }
                }
            }
        }
    }
}

subprojects {
    description = 'ItemRender Bukkit ' + project.name.substring(7)

    if (project.name != 'stickynote') {
        apply from: "$rootDir/gradle/bukkit-module.gradle"
    }
}

dependencies {
    api project(':api')
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                afterEvaluate {
                    description = 'ItemRender API for bukkit based projects'
                }
            }
        }
    }
}