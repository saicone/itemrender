plugins {
    id 'com.gradleup.shadow' version '8.3.5' apply false
}

description = 'ItemRender'

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'java-library'

    if (project.name != 'stickynote') {
        apply from: "$rootDir/gradle/publish-config.gradle"
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.codemc.io/repository/nms/' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://libraries.minecraft.net' }
    }

    dependencies {
        if (project.name != 'api') {
            compileOnlyApi project(':api')
        }

        compileOnly 'org.jetbrains:annotations:26.0.2'

        compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        withSourcesJar()
        withJavadocJar()
    }
}